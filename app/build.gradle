plugins {
    id 'com.android.application'
}

static def getProperty(String propertyFile, String propertyName) {
    Properties properties = new Properties()
    properties.load(new FileInputStream(propertyFile))
    return properties.getProperty(propertyName)
}

static String quoted(String value) {
    return "\"$value\""
}

android {
    compileSdk 31

    //noinspection GroovyAssignabilityCheck
    defaultConfig {
        applicationId "edu.cnm.deepdive.animals15"
        minSdk 26
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField 'String', 'BASE_URL',
                quoted(getProperty("$projectDir/local.properties", 'base_url'))
        buildConfigField 'String', 'CONTENT_FORMAT',
                quoted(getProperty("$projectDir/local.properties", 'content_format'))
    }

    //noinspection GroovyAssignabilityCheck
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    // Basic Android components
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'

    // Material Design components
    implementation 'com.google.android.material:material:1.5.0'

    // ReactiveX libraries
    implementation 'io.reactivex.rxjava3:rxjava:3.1.3'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'

    // Retrofit dependencies
    def retrofitVersion = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"

    // logging
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.3'

    // Gson (JSON parser) library
    implementation 'com.google.code.gson:gson:2.8.9'

    // Picasso library
    implementation 'com.squareup.picasso:picasso:2.71828'

    // Testing libraries
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
